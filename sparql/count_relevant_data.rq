PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX ps: <http://purl.org/phenoscape/vocab.owl#>
PREFIX ow: <http://purl.org/phenoscape/owlet/syntax#>
PREFIX StandardState: <http://purl.obolibrary.org/obo/CDAO_0000045>
PREFIX has_character: <http://purl.obolibrary.org/obo/CDAO_0000142>
PREFIX has_state: <http://purl.obolibrary.org/obo/CDAO_0000184>
PREFIX belongs_to_tu: <http://purl.obolibrary.org/obo/CDAO_0000191>
PREFIX has_external_reference: <http://purl.obolibrary.org/obo/CDAO_0000164>
PREFIX HasNumberOf: <http://purl.obolibrary.org/obo/PATO_0001555>
PREFIX Count: <http://purl.obolibrary.org/obo/PATO_0000070>
PREFIX part_of: <http://purl.obolibrary.org/obo/BFO_0000050>
PREFIX LimbFin: <http://purl.obolibrary.org/obo/UBERON_0004708>
PREFIX Sarcopterygii: <http://purl.obolibrary.org/obo/VTO_0001464>
PREFIX connected_to: <http://purl.obolibrary.org/obo/RO_0002170>
PREFIX GirdleSkeleton: <http://purl.obolibrary.org/obo/UBERON_0010719>
PREFIX inheres_in_part_of: <http://purl.obolibrary.org/obo/RO_0002314>
PREFIX hint: <http://www.bigdata.com/queryHints#>

SELECT ?matrix ?matrix_label (COUNT(DISTINCT ?matrix_char) AS ?char_count) (COUNT(DISTINCT ?state) AS ?state_count) (COUNT(DISTINCT ?phenotype) AS ?phenotype_count) (COUNT(DISTINCT ?taxon) AS ?taxon_count)
FROM <http://kb.phenoscape.org/>
WHERE
{	
    ?phenotype (ps:entity_term|ps:related_entity_term) ?entity .
    ?state ps:denotes_exhibiting/rdf:type ?phenotype .
    ?matrix_char ps:may_have_state_value ?state .
    ?matrix has_character: ?matrix_char .
    ?matrix rdfs:label ?matrix_label .
    ?state ^has_state:/belongs_to_tu:/has_external_reference: ?taxon .
    ?taxon rdfs:subClassOf "Sarcopterygii:"^^ow:omn .
    ?entity rdfs:subClassOf "part_of: some (LimbFin: or GirdleSkeleton:) or connected_to: some (LimbFin: or GirdleSkeleton:)"^^ow:omn .
} GROUP BY ?matrix ?matrix_label